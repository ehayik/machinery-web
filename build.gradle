buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'ru.vyarus.pom' version '2.1.0'
    id 'org.springframework.boot' version "${springBoot}"
    id 'io.spring.dependency-management' version "${dependencyManagement}"
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'pl.allegro.tech.build.axion-release' version '1.13.2'
    id 'org.sonarqube' version '3.2.0'
    id 'jacoco'
}

// see https://github.com/sherter/google-java-format-gradle-plugin/issues/58
if(JavaVersion.current() != JavaVersion.VERSION_1_8) {
    googleJavaFormat {
        options style: 'AOSP'
    }

    compileJava.dependsOn 'googleJavaFormat'
}

sonarqube {
    properties {
        property "sonar.projectKey", "eljaiek_machinery-web"
        property "sonar.organization", "eljaiek"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

project.version = scmVersion.version

allprojects {
    repositories {
        mavenCentral()
    }

    bootJar {
        enabled = false
    }
}

subprojects {
    group = 'com.github.eljaiek.machinery'
    version = rootProject.version

    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'ru.vyarus.pom'
    apply plugin: 'jacoco'
    apply from: "$rootDir/gradle/ext/publish-common.gradle"

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation(platform("org.junit:junit-bom:${junit}"))
    }

    test {
        useJUnitPlatform()
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    java {
        withSourcesJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
    
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test.finalizedBy jacocoTestReport

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom { x -> pom(x)}
            }
        }

        repositories {
            maven { x -> nexusRepository(x) }
        }
    }
}

task publishAll(type: GradleBuild) {
    tasks = [ 'publish' ]
}

task publishAllToMavenLocal(type: GradleBuild) {
    tasks = [ 'publishToMavenLocal' ]
}
